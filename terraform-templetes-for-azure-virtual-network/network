provider "azurerm" {
  features {}
  subscription_id = "**************************************"
}

# Create resource groups for each VNet
resource "azurerm_resource_group" "network" {
  for_each = local.network_spec

  name     = "${each.key}-rg"
  location = each.value.location
}

# Create virtual networks
resource "azurerm_virtual_network" "vnet" {
  for_each = local.network_spec

  name                = each.key
  resource_group_name = azurerm_resource_group.network[each.key].name
  location            = each.value.location
  address_space       = each.value.address_space
}

# Create subnets for each VNet
resource "azurerm_subnet" "subnet" {
  for_each = { for idx, subnet in flatten([
    for vnet_key, vnet in local.network_spec : [
      for s in vnet.subnets : {
        vnet_key       = vnet_key
        subnet_name    = "${vnet_key}-${s.name_suffix}"
        address_prefix = s.address_prefix
      }
    ]
  ]) : subnet.subnet_name => subnet }

  name                 = each.value.subnet_name
  resource_group_name  = azurerm_resource_group.network[each.value.vnet_key].name
  virtual_network_name = azurerm_virtual_network.vnet[each.value.vnet_key].name
  address_prefixes     = [each.value.address_prefix]
}
